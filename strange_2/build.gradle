plugins {
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.2.1'
    id 'signing'
    id 'maven'
}
apply from: 'gradle/mavenPublish.gradle'

group = 'com.gluonhq'
version = '0.0.3-SNAPSHOT'

mainClassName = 'com.gluonhq.strange.demo.Demo'

repositories {
    mavenCentral()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases/'
    }
}

def currentOS = org.gradle.internal.os.OperatingSystem.current();
def classifier
if (currentOS.isWindows()) {
    classifier = 'win'
} else if (currentOS.isLinux()) {
    classifier = 'linux'
} else if (currentOS.isMacOsX()) {
    classifier = 'mac'
}

dependencies {
    // compile "com.gluonhq:charm-cloudlink-client:4.4.7"
    // runtime "com.gluonhq:charm-down-plugin-storage-desktop:3.6.0"
    testCompile "junit:junit:4.12"
    compile "org.openjfx:javafx-controls:11-ea+23:$classifier"
    compile "org.openjfx:javafx-graphics:11-ea+23:$classifier"
    compile "org.openjfx:javafx-base:11-ea+23:$classifier"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

javadoc {
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

ext.publish = !project.version.endsWith('SNAPSHOT')

configurations {
    published
}


artifacts {
    archives sourceJar, javadocJar
}

