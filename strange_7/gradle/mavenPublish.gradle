apply plugin: 'signing'

configurations {
    samples { extendsFrom runtime}
    jdk
    maven { extendsFrom archives }
    published { extendsFrom archives, signatures}
}

signing {
    required = { gradle.taskGraph.hasTask(uploadPublished) && !version.endsWith("SNAPSHOT") }
    sign configurations.archives
}

repositories {
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/groups/staging' }
    mavenCentral()
}

uploadPublished {

    doFirst {
        // configure repositories in a doFirst so we can late bind the properties
        // use gradlew -PrepositoryUrl=https://oss.sonatype.org/service/local/staging/deploy/maven2 uploadPublished to upload release
        repositories {
            mavenDeployer {
                def repositoryUrl = getStringProperty('repositoryUrl', 'https://oss.sonatype.org/content/repositories/snapshots/')
                def repositoryUsername = getStringPropertyDefaultAsProperty('repositoryUsername', 'sonatypeUsername')
                def repositoryPassword = getStringPropertyDefaultAsProperty('repositoryPassword', 'sonatypePassword')

                configurePOM(pom)
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: repositoryUrl) {
                    authentication userName: repositoryUsername, password: repositoryPassword
                }
            }
        }
    }
    configuration = configurations.published
}

install {
    doFirst {
        configurePOM(repositories.mavenInstaller.pom)
    }
    configuration = configurations.published
}

private configurePOM(def pom) {
    pom.project {
        name 'Strange'
        description 'Java API for Quantum Computing'
        url 'https://github.com/gluonhq/strange'
        modelVersion '4.0.0'
        packaging 'jar'
        scm {
            connection 'scm:git:https://github.com/gluonhq/strange.git'
            developerConnection 'scm:git:ssh://git@github.com:gluonhq/strange.git'
            url 'https://github.com/gluonhq/strange'
        }
        developers {
            developer {
                name 'Johan Vos'
                email 'johan.vos@gluonhq.com'
                roles {
                    role 'author'
                    role 'developer'
                }
            }
        }
        licenses {
            license {
                name 'The 3-Clause BSD License'
                url 'http://www.opensource.org/licenses/bsd-license.php'
                distribution 'repo'
            }
        }
    }
}

private String getStringProperty(String name, String defaultValue) {
    if (!project.hasProperty(name)) {
        return defaultValue
    }

    def propertyValue = project.property(name)
    return propertyValue != null ? propertyValue.toString().trim() : defaultValue
}

private String getStringPropertyDefaultAsProperty(String name, String defaultProperty) {
    if (!project.hasProperty(name)) {
        return project.hasProperty(defaultProperty) ? project.property(defaultProperty) : System.getenv(name)
    }

    def propertyValue = project.property(name)
    if (propertyValue != null) {
        return propertyValue.toString().trim()
    } else {
        return project.hasProperty(defaultProperty) ? project.property(defaultProperty) : System.getenv(name)
    }
}
